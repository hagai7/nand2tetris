// This file is part of nand2tetris, as taught in The Hebrew University,
// and was written by Aviv Yaish, and is published under the Creative 
// Common Attribution-NonCommercial-ShareAlike 3.0 Unported License 
// https://creativecommons.org/licenses/by-nc-sa/3.0/
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // You're advised to work on the ALU chip in two steps:
   // - First, without handling status outputs (ALU-nostat)
   // - Then, adding the missing functionality for the "full" chip (ALU). 
   // You only need to submit the "full" ALU, no need to submit the partial
   // implementation (ALU-nostat).
   // Put you code here:
   
        //zx and nx:
        Mux16(a=x, b[0..15]=false, sel=zx, out=muxzx);
        Not16(in=muxzx, out=notx);
        Mux16(a=muxzx, b=notx,sel=nx, out=muxnx);

        //zy and ny:        
        Mux16(a=y, b[0..15]=false, sel=zy, out=muxzy);
        Not16(in=muxzy, out=noty);
        Mux16(a=muxzy, b=noty,sel=ny, out=muxny);

        //f and no:
        Add16(a=muxnx, b=muxny, out=fadd);
        And16(a=muxnx, b=muxny, out=fand);
        Mux16(a=fand, b=fadd, sel=f, out=fout);
        Not16(in=fout, out=notfout);
        
        //zr and ng:
        Mux16(a=fout, b=notfout, sel=no, out=out, out[15]=sign, out[0..7]=left, out[8..15]=right);
        Or8Way(in=right, out=zrright);
        Or8Way(in=left, out=zrleft);
        Or(a=zrleft, b=zrright, out=notzr);
        Not(in=notzr, out=zr);
        And(a=sign, b=true, out=ng);

}

